plugins {
    id "java"
    id "com.gradleup.shadow" version "8.3.5"
    id "xyz.jpenilla.run-velocity" version "2.3.1"
}

group = "dev.seeruk"
version = "1.0-SNAPSHOT"

base {
    archivesName = "seers-velocity-discord-chat"
}

dependencies {
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    annotationProcessor "com.velocitypowered:velocity-api:3.3.0-SNAPSHOT"
    compileOnly "com.velocitypowered:velocity-api:3.3.0-SNAPSHOT"

    shadow project(":common-chat")
    shadow project(":common-config")
    compileOnly project(":velocity-discord")

    shadow libs.lettuce.core
    compileOnly libs.jda
    compileOnly libs.protobuf.java
}

java {
    toolchain.setLanguageVersion(JavaLanguageVersion.of(17))
}

tasks {
    compileJava {
        dependsOn(":velocity-discord:shadowJar")
    }

    runVelocity {
        // Configure the Velocity version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin"s jar (or shadowJar if present) will be used automatically.
        velocityVersion("3.3.0-SNAPSHOT")
    }

    shadowJar {
        configurations = [project.configurations.shadow]
        archiveClassifier = ""
    }

    build {
        dependsOn(shadowJar)
    }
}

def templateSource = file("src/main/templates")
def templateDest = layout.buildDirectory.dir("generated/sources/templates")
def generateTemplates = tasks.register("generateTemplates", Copy) { task ->
    def props = [
            "version": project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates
